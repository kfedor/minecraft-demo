plugins {
    id 'java'
    id 'fabric-loom' version '1.10.1'
    id 'com.google.protobuf' version '0.9.4'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.github.kfedor.minecraft'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

dependencies {
    minecraft "com.mojang:minecraft:1.21.8"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:0.16.7"
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.136.0+1.21.8"

    shade 'com.google.protobuf:protobuf-java:4.28.2'
    shade "org.postgresql:postgresql:42.7.8"
    shade "com.zaxxer:HikariCP:7.0.2"

    testImplementation "org.junit.jupiter:junit-jupiter:5.11.3"
    testImplementation "org.testcontainers:junit-jupiter:1.20.3"
    testImplementation "org.testcontainers:postgresql:1.20.3"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:4.28.2"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    outputSubDir = "generated-sources/protobuf/java"
                }
            }
        }
    }
}

tasks.withType(ProcessResources).configureEach {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.shadowJar {
    configurations = [project.configurations.shade]
    zip64 true
}

tasks.build.dependsOn tasks.shadowJar

test {
    useJUnitPlatform()
}

